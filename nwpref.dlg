
VERSION 10.0
SYNCHRONOUS_ONLY
INCLUDE _std_medium
INCLUDE _ht350
INCLUDE header

HELP hlp1, view("help nwpref")
RESET res1

SCRIPT PREINIT
BEGIN
		program initialize
END

SCRIPT POSTINIT
BEGIN
		main.ed_new.setvalue "_pref"
END

DIALOG main, title("nwpref - Generate Preferential Attachment Network")
BEGIN
	
	TEXT tx_nets 220 30 130 ., label("Number of networks:")
	SPINNER sp_nets 350 30 50 ., label("Networks") max(999) default(1) option(ntimes)
	
	GROUPBOX gb_ties 220 60 160 100, label("Arc/edge")
	RADIO rb_directed 240 90 100 ., label("Directed") first
	RADIO rb_undirected 240 110 120 ., label("Undirected") last option(undirected)
	
	TEXT tx_nodes 40 30 100 ., label("Number of nodes:")
	SPINNER sp_nodes 160 30 50 ., label("Nodes") max(9999) default(10)
	
	TEXT tx_m0 40 60 100 ., label("Start nodes (m0):")
	SPINNER sp_m0 160 60 50 ., label("M0") default(4) option(m0)
	
	TEXT tx_m 40 90 130 ., label("New connections (m):")
	SPINNER sp_m 160 90 50 ., label("M") default(4) option(m)
	
	TEXT tx_prob 40 120 200 ., label("Connect at random (prob):")
	TEXT tx_prob2 90 140 200 ., label("Probability .")
	SPINNER sp_prob 160 140 50 ., label("prob")	
	
	INCLUDE nw_newnet_opt
END

LIST netlist
BEGIN
	// intentionally empty
	// this list will be populated by _nwdialog.ado
END

PROGRAM initialize
BEGIN
	put "_nwdialog nwpref"
	stata hidden immediate
END

PROGRAM command
BEGIN

	put "nwpref " main.sp_nodes 
	beginoptions
		optionarg main.sp_m0
		optionarg main.sp_m
		put " prob(." main.sp_prob ")"
		optionarg main.sp_nets
		option main.rb_undirected
		option main.cx_xvars
		optionarg main.ed_new
		optionarg main.ed_labs 
		optionarg main.ed_vars
	endoptions
	stata
END
